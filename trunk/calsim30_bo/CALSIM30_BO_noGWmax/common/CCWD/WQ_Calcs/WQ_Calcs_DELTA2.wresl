!Define WQ (salinity) decision variables by node

define WQ420 {std kind 'salinity' units 'mg/l'}
define WQ421 {std kind 'salinity' units 'mg/l'}
!define WQ422 {std kind 'salinity' units 'mg/l'}
define WQ423 {std kind 'salinity' units 'mg/l'}
define WQ424 {std kind 'salinity' units 'mg/l'}

!Calculate water quality

!Old River Pipeline
define scale_421 {
	case no_water {
                condition       D408_OR[DELTA2] + D408_VC[DELTA2] < 0.01
		value		0.
	}
	case otherwise {
		condition	always
                value           1./(D408_OR[DELTA2] + D408_VC[DELTA2])
	}
}
goal calc_WQ421 {WQ421 = WQ408_OR*D408_OR[DELTA2]*scale_421 + WQ408_VC*D408_VC[DELTA2]*scale_421}

!Transfer Facility
define scale_423 {
	case no_water {
                condition       C421[DELTA2] + C423_FILL[DELTA2] + C422_REL[DELTA2] < 0.01
		value		0.
	}
	case otherwise {
		condition	always
                value           1./(C421[DELTA2] + C423_FILL[DELTA2] + C422_REL[DELTA2])
	}
}
goal calc_WQ423 {WQ423 = WQ421*C421[DELTA2]*scale_423 + WQ424_MOK*C423_FILL[DELTA2]*scale_423 + WQ422(-1)*C422_REL[DELTA2]*scale_423}

!Los Vaqueros

define scale_422 {
	case no_water {
                condition       S422[DELTA2]*taf_cfs < 0.01
		value		0.
	}
	case otherwise {
		condition	always
                value           1./(S422[DELTA2]*taf_cfs)
	}
}

goal calc_WQ422 {WQ422 = WQ422(-1)*S422(-1)*taf_cfs*scale_422 - WQ422(-1)*C422_REL[DELTA2]*scale_422 - WQ422(-1)*C422_KC[DELTA2]*scale_422
                        + WQ422_inflow*I422*scale_422 + WQ422_ppt*I422_ppt_cfs*scale_422 + WQ423*C422_FILL[DELTA2]*scale_422}


!Los Vaqueros Pipeline at Mok Aq. Intertie
define scale_424 {
	case no_water {
                condition       I424[DELTA2] + C423_DEL[DELTA2] < 0.01
		value		0.
	}
	case otherwise {
		condition	always
                value           1./(I424[DELTA2] + C423_DEL[DELTA2])
	}
}

goal calc_WQ424 {WQ424 = WQ424_MOK*I424[DELTA2]*scale_424 + WQ423*C423_DEL[DELTA2]*scale_424}

!Contra Costa Canal downstream of Mallard Slough intake

define scale_420 {
	case no_water {
                condition       C424[DELTA2] + D408_RS[DELTA2] + D406A_MS[DELTA2] < 0.01
		value		0.
	}
	case otherwise {
		condition	always
                value           1./(C424[DELTA2] + D408_RS[DELTA2] + D406A_MS[DELTA2])
	}
}

goal calc_WQ420 {WQ420 = WQ424*C424[DELTA2]*scale_420 + WQ408_RS*D408_RS[DELTA2]*scale_420 + WQ406A_MS*D406A_MS[DELTA2]*scale_420}
